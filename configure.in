dnl Configure script for the Eye of Gnome image viewer

AC_INIT(shell/main.c)

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(eog, 0.7)

AM_MAINTAINER_MODE

GNOME_PLATFORM_GNOME_2(yes)

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS

dnl ===============================================================================

ALL_LINGUAS="az ca cs da de el es et fi fr ga gl hu it ja ko lt nl nn no pl pt_BR ro ru sl sv tr uk zh_CN.GB2312 zh_TW"
AM_GLIB_GNU_GETTEXT
AC_PROG_INTLTOOL

dnl ===============================================================================

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

ORBIT_IDL="`$PKG_CONFIG --variable=orbit_idl ORBit-2.0`"
AC_SUBST(ORBIT_IDL)

BONOBO_IDLDIR="`$PKG_CONFIG --variable=idldir libbonobo-2.0`"
AC_SUBST(BONOBO_IDLDIR)

BONOBO_ACT_IDLDIR="`$PKG_CONFIG --variable=idldir bonobo-activation-2.0`"
AC_SUBST(BONOBO_ACT_IDLDIR)


GCONF_CONFIG_SOURCE=
AC_ARG_ENABLE(gconf-source, 
            [  --enable-gconf-source=sourceaddress      Where to install schema files.],GCONF_CONFIG_SOURCE=$enable_gconf_source,)

AC_DEFUN([AC_FYI], [echo "FYI: " $1])

# find the actual value for $prefix that we'll end up with
REAL_PREFIX=
if test "x$prefix" = "xNONE"; then
  REAL_PREFIX=$ac_default_prefix
else
  REAL_PREFIX=$prefix
fi

# Have to go $sysconfdir->$prefix/etc->/usr/local/etc   
# if you actually know how to code shell then fix this :-)
SYSCONFDIR_TMP="$sysconfdir"
old_prefix=$prefix
prefix=$REAL_PREFIX
EXPANDED_SYSCONFDIR=`eval echo $SYSCONFDIR_TMP`
prefix=$old_prefix
AC_SUBST(EXPANDED_SYSCONFDIR)

if test "x$GCONF_CONFIG_SOURCE" = "x"; then
        GCONF_CONFIG_SOURCE="xml::$EXPANDED_SYSCONFDIR/gconf/gconf.xml.defaults"
        AC_FYI("Using default config source $GCONF_CONFIG_SOURCE for schema installation")
else
        AC_FYI("Using config source $GCONF_CONFIG_SOURCE for schema installation")
fi

AC_SUBST(GCONF_CONFIG_SOURCE)

AC_ARG_ENABLE(bonobo-filesel,
[  --enable-bonobo-filesel=[no/yes]    Use the new bonobo file selector.],bonobo_filesel="$enableval",bonobo_filesel=no)
if test "x$bonobo_filesel" = "xyes"; then
	AC_DEFINE(ENABLE_BONOBO_FILESEL)
fi

dnl ******************************
dnl Check for evolution
dnl ******************************
try_evolution=true
evolution=no
AC_ARG_WITH(evolution,
	[  --{with,without}-evolution          Use evolution to send images.],
	if test x$withval = xno; then
		try_evolution=false
	fi
)
if $try_evolution; then
	path_to_composer_idl=`gnome-config --datadir`/idl/Evolution-Composer.idl
	AC_CHECK_FILE($path_to_composer_idl, evolution=yes, evolution=no)
	if test x"$evolution" = "xyes"; then
		AC_DEFINE(ENABLE_EVOLUTION)
	fi
fi
AM_CONDITIONAL(ENABLE_EVOLUTION, test x"$evolution" = "xyes")

dnl ******************************
dnl Check for GnoCam
dnl ******************************
try_gnocam=true
gnocam=no
AC_ARG_WITH(gnocam,
	[  --{with,without}-gnocam             Use gnocam to acquire images.],
	if test x$withval = xno; then
		try_gnocam=false
	fi
)
if $try_gnocam; then
	path_to_gnocam_idl=`gnome-config --datadir`/idl/GnoCam.idl
	AC_CHECK_FILE($path_to_gnocam_idl, gnocam=yes, gnocam=no)
	if test x"$gnocam" = "xyes"; then
		AC_DEFINE(ENABLE_GNOCAM)
	fi
fi
AM_CONDITIONAL(ENABLE_GNOCAM, test x"$gnocam" = "xyes")


EXTRA_GNOME_MODULES="gnome-vfs-2.0 gdk-pixbuf-2.0 libgnomecanvas-2.0 libgnomeui-2.0 libbonobo-2.0 libbonoboui-2.0 libgnomeprint-2.0 bonobo-activation-2.0"
EXTRA_GNOME_CFLAGS=`$PKG_CONFIG --cflags $EXTRA_GNOME_MODULES`
EXTRA_GNOME_LIBS=`$PKG_CONFIG --libs $EXTRA_GNOME_MODULES`
GNOME_CFLAGS="$EXTRA_GNOME_CFLAGS"
GNOME_LIBS="$EXTRA_GNOME_LIBS"

AC_SUBST(EXTRA_GNOME_CFLAGS)
AC_SUBST(EXTRA_GNOME_LIBS)
AC_SUBST(GNOME_CFLAGS)
AC_SUBST(GNOME_LIBS)

dnl
dnl ******************************
dnl Image Libraries
dnl ******************************
jpeg_ok=no
dnl Test for libjpeg
  if test -z "$LIBJPEG"; then
    AC_CHECK_LIB(jpeg, jpeg_destroy_decompress,
      jpeg_ok=yes,
      jpeg_ok=no
      AC_MSG_WARN(*** JPEG saving code will not be built (JPEG library not found) ***))
    if test "$jpeg_ok" = yes; then
      AC_MSG_CHECKING([for jpeglib.h])
      AC_TRY_CPP(
[#include <stdio.h>
#undef PACKAGE
#undef VERSION
#undef HAVE_STDDEF_H
#undef HAVE_STDLIB_H
#include <jpeglib.h>],
        jpeg_ok=yes,
        jpeg_ok=no)
      AC_MSG_RESULT($jpeg_ok)
      if test "$jpeg_ok" = yes; then
        LIBJPEG='-ljpeg'
        AC_CHECK_LIB(jpeg, jpeg_simple_progression,     
          AC_DEFINE(HAVE_PROGRESSIVE_JPEG),
          AC_MSG_WARN(JPEG library does not support progressive saving.))
      else
          AC_MSG_WARN(*** JPEG saving code will not be built (JPEG header file not found) ***)
      fi
    fi
  fi
if test "x$jpeg_ok" = "xyes" ; then
  AC_DEFINE(HAVE_JPEG)
fi

png_ok=no
dnl Test for libpng
  if test -z "$LIBPNG"; then
    AC_CHECK_LIB(png, png_read_info,
      AC_CHECK_HEADER(png.h,
        png_ok=yes,
        png_ok=no),
      AC_MSG_WARN(*** PNG saving code will not be built (PNG library not found) ***), -lz -lm)
    if test "$png_ok" = yes; then
      AC_MSG_CHECKING([for png_structp in png.h])
      AC_TRY_COMPILE([#include <png.h>],
        [png_structp pp; png_infop info; png_colorp cmap; png_create_read_struct;],
        png_ok=yes,
        png_ok=no)
      AC_MSG_RESULT($png_ok)
      if test "$png_ok" = yes; then
        PNG='png'; LIBPNG='-lpng -lz'
      else
        AC_MSG_WARN(*** PNG saving code will not be built (PNG library is too old) ***)
      fi
    else
     AC_MSG_WARN(*** PNG saving code will not be built (PNG header file not found) ***)
    fi
  fi
if test "x$png_ok" = "xyes" ; then
  AC_DEFINE(HAVE_PNG)
fi

AC_SUBST(LIBJPEG)
AC_SUBST(LIBPNG)


dnl ******************************
dnl More compiler warnings
dnl ******************************

AC_ARG_ENABLE(more-warnings,
[  --enable-more-warnings  Maximum compiler warnings],
set_more_warnings="$enableval",[set_more_warnings=no])
warning_flags=
realsave_CFLAGS="$CFLAGS"
AC_MSG_CHECKING(for more warnings, including -Werror)
if test "$GCC" = "yes" -a "$set_more_warnings" != "no"; then
	AC_MSG_RESULT(yes)
	warning_flags="-Wall -Wchar-subscripts -Wmissing-declarations -Wmissing-prototypes -Wnested-externs -Wpointer-arith -Werror"
	CFLAGS="$warning_flags $CFLAGS"

	for option in -Wsign-promo -Wno-sign-compare; do
		SAVE_CFLAGS="$CFLAGS"
		CFLAGS="$CFLAGS $option"
		AC_MSG_CHECKING([whether gcc understands $option])
		AC_TRY_COMPILE([], [],
			has_option=yes,
			has_option=no,)
		CFLAGS="$SAVE_CFLAGS"
		AC_MSG_RESULT($has_option)
		if test $has_option = yes; then
		  warning_flags="$warning_flags $option"
		fi
		unset has_option
		unset SAVE_CFLAGS
	done
	unset option
else
	AC_MSG_RESULT(no)
fi
CFLAGS="$realsave_CFLAGS"
EXTRA_WARNING_CFLAGS="$warning_flags"
AC_SUBST(EXTRA_WARNING_CFLAGS)

AC_OUTPUT([
Makefile
eog.spec
art/Makefile
libeog/Makefile
libeog/cursors/Makefile
libeog/stock/Makefile
viewer/Makefile
idl/Makefile
doc/Makefile
doc/C/Makefile
shell/Makefile
collection/Makefile
intl/Makefile
po/Makefile.in
])



echo "
Configuration:

	Source code location:	  ${srcdir}
	Compiler:		  ${CC} 

	Use Bonobo File Selector: $bonobo_filesel
	Use Evolution:            $evolution
	Use GnoCam:               $gnocam
	Extra Compiler Warnings:  ${EXTRA_WARNING_CFLAGS}
	
"
