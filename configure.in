dnl Configure script for the Eye of Gnome image viewer

AC_INIT(src/render.c)

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(eog, 0.2)

AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS

ALL_LINGUAS="cs da de el es et fr ga gl it ja ko no pl ru sv tr uk hu"
AM_GNU_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl ******************************
dnl Check for Bonobo
dnl ******************************
try_bonobo=true
bonobo=
bonobo_msg=no
have_bonobo=false
AC_ARG_WITH(bonobo,
	[--{with,without}-bonobo   Compile with Bonobo support or without it],
	if test x$withval = xno; then
		try_bonobo=false
	fi
)

gnumeric_executable=gnumeric
AC_SUBST(gnumeric_executable)
if $try_bonobo; then
	AC_MSG_CHECKING(for Bonobo > 0.7)
	if gnome-config --libs bonobo > /dev/null 2>&1; then
		vers=`gnome-config --modversion bonobo`
		case $vers
		in
		    bonobo-0.[[01234]]) bonobo_ok=false ;;
		    *) bonobo_ok=true ;;
		esac
	else
		bonobo_ok=false
	fi
	
	if $bonobo_ok; then
		AC_MSG_RESULT(found)
		AC_DEFINE(ENABLE_BONOBO)
		have_bonobo=true
		bonobo=bonobo
		bonobo_msg=yes
	        gnumeric_executable=gnumeric-bonobo
	else
		AC_MSG_RESULT(not found)
	fi
fi
AM_CONDITIONAL(BONOBO, $have_bonobo)

EXTRA_BONOBO_CFLAGS=`gnome-config --cflags gnomeui gdk_pixbuf gnomecanvaspixbuf $bonobo`
EXTRA_BONOBO_LIBS=`gnome-config --libs gnomeui gdk_pixbuf gnomecanvaspixbuf $bonobo`
AC_SUBST(EXTRA_BONOBO_CFLAGS)
AC_SUBST(EXTRA_BONOBO_LIBS)

EXTRA_GNOME_CFLAGS=`gnome-config --cflags gnomeui gdk_pixbuf gnomecanvaspixbuf`
EXTRA_GNOME_LIBS=`gnome-config --libs gnomeui gdk_pixbuf gnomecanvaspixbuf`
AC_SUBST(EXTRA_GNOME_CFLAGS)
AC_SUBST(EXTRA_GNOME_LIBS)

AC_OUTPUT([
Makefile
intl/Makefile
macros/Makefile
po/Makefile.in
src/Makefile
src/cursors/Makefile
src/stock/Makefile
bonobo/Makefile
])

echo "
Configuration:

	Source code location:	${srcdir}
	Compiler:		${CC} 
	
	Bonobo Support:		${bonobo_msg}
"
